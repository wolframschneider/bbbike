#!/usr/bin/perl -w

use strict;

use FindBin;
use lib ("$FindBin::RealBin/..",
	 "$FindBin::RealBin/../lib",
	);
use Getopt::Long;
use Strassen::Core;
use Strassen::GeoJSON;

sub usage {
    die <<EOF;
usage: $0 [-compact|-nocompact|-multiline] [-bbbgeojsonp] bbdfile
EOF
}

my $pretty = 1;
my $multiline = 0;
my $utf8 = 0;
my $combine;
my $bbbgeojsonp;
GetOptions(
	   "compact"     => sub {
	       $multiline = 0;
	       $pretty = 0;
	       $utf8 = 1;
	   },
	   "multiline"   => sub {
	       $multiline = 1;
	       $pretty = 0;
	       $utf8 = 1;
	   },
	   "combine!"     => \$combine,
	   "bbbgeojsonp!" => \$bbbgeojsonp,
	  ) or usage();

my $file = shift || usage();
my $s = Strassen->new($file);
my $s_geojson = Strassen::GeoJSON->new($s);
if ($bbbgeojsonp) {
    print "// generated with bbd2geojson from BBBike distribution, see\n";
    print "// https://github.com/eserte/bbbike/blob/master/miscsrc/bbd2geojson\n";
}
print $s_geojson->bbd2geojson(bbbgeojsonp => $bbbgeojsonp, pretty => $pretty, utf8 => $utf8, multiline => $multiline, combine => $combine);
print "\n";

__END__

=head1 NAME

bbd2geojson - convert BBD files to GeoJSON files

=head1 SYNOPSIS

    bbd2geojson [-compact|-nocompact|-multiline] [-combine] [-bbbgeojsonp] bbdfile > output.geo.json

=head1 DESCRIPTION

Create a GeoJSON file from a BBBike data file (bbd). Options:

=over

=item C<-compact>

Create somewhat smaller output: don't create a "pretty" json,
resulting in a one-line (and diff-unfriendly) file, and use utf-8
encoding instead of javascript escape sequences (which are usually
some bytes longer).

=item C<-multiline>

For GeoJSON with a FeatureCollection: put every feature in a separate
line. This might generate diff- and rsync-friendly output, but still
quite compact in contract to the default output style.

Create

=item C<-combine>

Combine features with same coordinates into a single feature, joining
the names (separated with a HTML C<< <br/> >>).

=item C<-bbbgeojsonp>

Create a JSONP-like file. For details, see L<Strassen::GeoJSON/JSONP
support>.

=back

=head1 AUTHOR

Slaven Rezic

=head1 SEE ALSO

L<Strassen::GeoJSON>.

=cut
