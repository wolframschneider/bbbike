#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: cvsdiffbbd,v 1.5 2007/05/08 22:00:08 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2007 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use Getopt::Long;

Getopt::Long::Configure('pass_through');

my $include_orig;
GetOptions("include-orig!" => \$include_orig,
	   "help|?" => sub { show_usage() },
	  );

my @cmd;
if (-d "RCS") {
    @cmd = qw(rcsdiff);
    die "RCS support is NYI";
} else {
    @cmd = qw(cvs diff);
}

push @cmd, @ARGV;

use constant STAGE_SHOW => 1;
use constant STAGE_HIDE => 2;
my $stage = STAGE_SHOW;
my $current_file;

my $bbdrx = qr{^> (\S+)\t(\S+)\s([ 0-9,.+-]+)$};

for (`@cmd`) {
    my $new_file_started = 0;
    if (/^Index:\s+(.*)/) {
	$current_file = $1;
	$new_file_started = 1;
    }

    if ($new_file_started) {
	if (!$include_orig) {
	    $stage = $current_file =~ /-orig/ ? STAGE_HIDE : STAGE_SHOW;
	}
	if ($current_file =~ /(bbbike-temp-blockings-optimized\.pl
			      |\.coords\.data
			      |Makefile
			      )/x) {
	    $stage = STAGE_HIDE;
	}
    }
    if ($stage == STAGE_SHOW) {
	if ($new_file_started) {
	    print "# File: $current_file\n";
	} elsif (s{$bbdrx}{$1\t#000080 $3}o) {
	    print;
	}
    }
}

sub show_usage {
    print STDERR <<EOF;
Usage example: $0 -D2007/05/01 > /tmp/20070501.bbd

EOF
    exit 0;
}
__END__


