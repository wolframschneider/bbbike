#!/usr/bin/perl -w
# -*- perl -*-

#
# Author: Slaven Rezic
#
# Copyright (C) 2007,2014 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use Getopt::Long;

Getopt::Long::Configure('pass_through');

my $include_orig;
my $diff_file;
GetOptions("include-orig!" => \$include_orig,
	   "diff-file=s"   => \$diff_file,
	   "help|?" => sub { show_usage() },
	  );

my @cmd;
if ($diff_file) {
    @cmd = ('cat', $diff_file);
    @ARGV = ();
} elsif (-d "RCS") {
    @cmd = qw(rcsdiff);
    die "RCS support is NYI";
} else {
    @cmd = qw(cvs diff);
}
push @cmd, @ARGV;

use constant STAGE_SHOW => 1;
use constant STAGE_HIDE => 2;
my $stage = STAGE_SHOW;
my $current_file;

my $bbdrx = qr{^
	       (?:>[ ]|\+)     # context or unified diff marker
	       ([^\t]*)        # name
	       \t(\S+)         # cat
	       \s([ 0-9,.+-]+) # coords
	       $}x;

for my $line (`@cmd`) {
    chomp $line;
    my $new_file_started = 0;
    if ($line =~ /^Index:\s+(.*)/) { # old CVS diff style
	$current_file = $1;
	$new_file_started = 1;
    } elsif ($line =~ m{^diff.*? a/(\S+) b/\S+}) { # new git diff style
	$current_file = $1;
	$new_file_started = 1;
    } elsif ($line =~ m{^diff.* (\S+) (\S+)$}) { # traditional diff
	$current_file = $1;
	$new_file_started = 1;
    }

    if ($new_file_started) {
	if ($current_file =~ m{(bbbike-temp-blockings-optimized\.pl
			       |sehenswuerdigkeit_img/
			       |\.coords\.data
			       |Makefile
			       |\.modified
			       |BASE
			       )}x) {
	    $stage = STAGE_HIDE;
	} elsif (!$include_orig && $current_file =~ /-orig/) {
	    $stage = STAGE_HIDE;
	} else {
	    $stage = STAGE_SHOW;
	}
    }
    if ($stage == STAGE_SHOW) {
	if ($new_file_started) {
	    print "# File: $current_file\n";
	} elsif ($line =~ m{$bbdrx}o) {
	    my $name   = $1;
	    my $cat    = "#000080";
	    my $coords = $3;
	    print "$name\t$cat $coords\n";
	}
    }
}

sub show_usage {
    print STDERR <<'EOF';
Usage examples:
    cd .../bbbike
    git diff -c 'master@{1 month ago}'..  -- data | ./miscsrc/cvsdiffbbd --diff-file - > /tmp/diff_last_month.bbd

EOF
    exit 0;
}
__END__

=head1 EXAMPLES

Create per-month diffs since beginning of time:

    cd .../bbbike
    for y in `seq 2003 2014`; do for m in 01 02 03 04 05 06 07 08 09 10 11 12; do echo $y$m; git diff $(git rev-list -n1 --before="$y-$m-01 00:00:00" master)..$(git rev-list -n1 --before="$y-$m-$(perl -MDate::Calc=Days_in_Month -e "print Days_in_Month(@ARGV)" $y $m) 23:59:59" master) -- data | ./miscsrc/cvsdiffbbd --diff-file - >| /tmp/diff-$y$m.bbd; done; done

Like before, but restrict to changes relevant for routing (routing
algorithm and routing instructions):

    cd .../bbbike
    for y in `seq 2003 2014`; do for m in 01 02 03 04 05 06 07 08 09 10 11 12; do echo $y$m; git diff $(git rev-list -n1 --before="$y-$m-01 00:00:00" master)..$(git rev-list -n1 --before="$y-$m-$(perl -MDate::Calc=Days_in_Month -e "print Days_in_Month(@ARGV)" $y $m) 23:59:59" master) -- data/strassen data/plaetze data/landstrassen data/landstrassen2 data/faehren data/hoehe data/orte data/orte2 data/orte_city data/ampeln data/qualitaet_s data/qualitaet_l data/handicap_s data/handicap_l data/nolighting data/green data/gesperrt data/comments_cyclepath data/comments_ferry data/comments_misc data/comments_mount data/comments_path data/comments_route data/comments_tram data/comments_kfzverkehr data/comments_scenic data/fragezeichen data/culdesac data/radwege_exact data/mount data/temp_blockings/bbbike-temp-blockings.pl data/inaccessible_strassen data/inaccessible_landstrassen | ./miscsrc/cvsdiffbbd --diff-file - >| /tmp/diff-routing-$y$m.bbd; done; done

Create a gif animation out of it:

    for i in /tmp/diff-2*bbd; do mkdir $(echo $i | sed 's/.bbd//'); done
    for i in /tmp/diff-2*bbd; do cp $i $(echo $i | sed 's/.bbd//')/strassen; done
    for i in /tmp/diff-2*bbd; do ./miscsrc/bbbikedraw.pl -bg '#000000' -bbox -10984,-1574,35441,32759 -datadirs $(echo $i | sed 's/.bbd//') -o $(echo $i | sed 's/.bbd/.png/'); done
    convert /tmp/diff-2*.png /tmp/diff.gif
    display /tmp/diff.gif

Create diff of converted Berlin OSM data and send to bbbike (if
started with -server mode):

    cd .../bbbike
    diff -ur "--exclude=_*" .../bbbike-old-osm-data .../bbbike-new-osm-data | (echo "#: encoding: utf-8"; echo "#: "; ./miscsrc/cvsdiffbbd --diff-file=-) >| /tmp/osm.diff.bbd
    ./bbbikeclient /tmp/osm.diff.bbd

Old outdated example with CVS repository:

    for y in 2004 2005 2006 2007; do for m in 01 02 03 04 05 06 07 08 09 10 11 12; do echo $y$m; ~/src/bbbike/miscsrc/cvsdiffbbd -D$y/$m/01 -D$y/$m/`perl -MDate::Calc=Days_in_Month -e "print Days_in_Month(@ARGV)" $y $m` >| /tmp/$y$m.bbd; done;done

=cut
