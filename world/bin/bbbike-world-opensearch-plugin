#!/usr/bin/perl
# (c) 2009 Wolfram Schneider
#
# bbbike-world-opensearch-plugin - create an opensearch plugin for  BBBike @ World cities
#

use IO::File;
use strict;
use warnings;

my $debug = 1;

sub usage () {
    <<EOF;
usage: $0 directory bbbike-db

EOF
}

sub extract_coords {
    my $db = shift;

    my @data;

    warn "Open $db\n" if $debug;
    my $fh = new IO::File $db, "r" or die "open: $!\n";
    while (<$fh>) {
        chomp;
        s/^\s+//;
        next if /^#/ || $_ eq "";

        my ( $city, $name, $lang, $area, $coord ) = split(/:/);
        my ( $x1, $y1, $x2, $y2 ) = split( /\s+/, $coord );

        my $x = ( $x2 - $x1 ) / 2 + $x1;
        my $y = ( $y2 - $y1 ) / 2 + $y1;

        next if $city eq 'dummy';
        $name = $city if !$name;

        push( @data, [ $city, $name, $lang, $x, $y ] );

    }
    close $fh;

    return @data;
}

sub create_opensearch_file {
    my %args = @_;

    my $file = $args{'file'} or die "no file given\n";
    my $city = $args{'city'} or die "no city given\n";
    my $name = $args{'name'} || $city;
    my $lang = $args{'lang'} || "de";

    warn "Create opensearch file $file\n" if $debug;
    my $fh = new IO::File $file, "w" or die "open '$file': $!\n";

    print $fh <<EOF;
<?xml version="1.0"?>
<OpenSearchDescription xmlns="http://a9.com/-/spec/opensearch/1.1/"
xml:lang="en">
  <ShortName>BBBike @ $city</ShortName>
  <LongName>BBBike @ <![CDATA[$name]]</LongName>
  <Description>BBBike @ World</Description>
  <Url template="http://www.bbbike.org/cgi/$city.$lang.cgi?sourceid=opensearch&amp;start={searchTerms}" type="text/html" />
  <Developer>Wolfram Schneider, Slaven Rezi&#x0107;</Developer>
  <Contact>http://www.bbbike.org/</Contact>
  <Image height="16" type="image/png" width="16">http://www.bbbike.org/images/srtbike16.gif</Image>
  <Language>$lang</Language>
</OpenSearchDescription>
EOF

    close $fh;

}

sub bbbike2opensearch {
    my %args = @_;
    my $dir  = $args{'dir'};
    my @data = @{ $args{'data'} };

    for my $d (@data) {
        my ( $city, $name, $lang, @rest ) = @$d;
        create_opensearch_file(
            'file' => "$dir/$city.xml",
            'city' => $city,
            'name' => $name,
            'lang' => $lang
        );
    }
}

my ( $dir, $db ) = @ARGV;
die &usage if !$db || !$dir;

my @data = &extract_coords($db);
&bbbike2opensearch( 'dir' => $dir, 'data' => \@data );

