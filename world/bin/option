#!/usr/local/bin/perl
# Copyright (c) 2009-2010 Wolfram Schneider, http://bbbike.org
#
# option -  create list of cities with links

use Getopt::Long;
use strict;
use warnings;

my $debug     = 1;      # 0: quiet, 1: normal, 2: verbose
my $city_lang = 'de';

sub usage () {
    <<EOF;
usage: $0 [--debug={0..2}] [ --lang lang ] < population.csv

--debug=0..2    debug option
--lang=		de | en
EOF
}

# select city name by language
sub select_city_name {
    my $city      = shift;
    my $name      = shift or die "No city name given!\n";
    my $city_lang = shift || "de";

    my %hash;
    $hash{ALL} = $city;
    foreach my $n ( split /\s*,\s*/, $name ) {
        my ( $lang, $city_name ) = split( /!/, $n );
        if ($city_name) {
            $hash{$lang} = $city_name;
        }

        # no city language defined, use default
        else {
            $hash{ALL} = $lang;
        }
    }

    #use Data::Dumper; warn Dumper( \%hash );
    return exists( $hash{$city_lang} ) ? $hash{$city_lang} : $hash{ALL};
}

GetOptions(
    "debug=i" => \$debug,
    "lang=s"  => \$city_lang,
) or die usage;

my %hash;
while (<>) {
    chomp;
    next if /^\s*#/;

    my ( $city, $name, $lang, $local_lang, $area, $coord, $population ) =
      split(/:/);
    next if $city eq 'dummy';

    $lang = "de"  if !$lang;
    $name = $city if !$name;

    $name = &select_city_name( $city, $name, $city_lang );

    my $link = "$city" . ( $lang eq 'de' ? '' : ".$lang" ) . '.cgi';
    $hash{$name} = $link;
}

foreach my $city ( sort keys %hash ) {
    print qq[<option value="], $hash{$city}, qq[">$city</option>\n];
}

