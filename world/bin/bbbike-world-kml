#!/usr/local/bin/perl
# Copyright (c) 2009-2010 Wolfram Schneider, http://bbbike.org
#
# bbbike-world-kml - create an Google Earth KML file for all BBBike @ World cities

use IO::File;
use strict;
use warnings;

sub usage () {
    <<EOF;
usage: $0 bbbike-db

EOF
}

# select city name by language
sub select_city_name {
    my $city      = shift;
    my $name      = shift or die "No city name given!\n";
    my $city_lang = shift || "de";

    my %hash;
    $hash{ALL} = $city;
    foreach my $n ( split /\s*,\s*/, $name ) {
        my ( $lang, $city_name ) = split( /!/, $n );
        if ($city_name) {
            $hash{$lang} = $city_name;
        }

        # no city language defined, use default
        else {
            $hash{ALL} = $lang;
        }
    }

    #use Data::Dumper; warn Dumper( \%hash );
    return exists( $hash{$city_lang} ) ? $hash{$city_lang} : $hash{ALL};
}

sub extract_coords {
    my $db = shift;

    my @data;

    my $fh = new IO::File $db, "r" or die "open: $!\n";
    while (<$fh>) {
        chomp;
        s/^\s+//;
        next if /^#/ || $_ eq "";

        my ( $city, $name, $lang, $area, $coord ) = split(/:/);
        my ( $x1, $y1, $x2, $y2 ) = split( /\s+/, $coord );

        my $x = ( $x2 - $x1 ) / 2 + $x1;
        my $y = ( $y2 - $y1 ) / 2 + $y1;

        next if $city eq 'dummy';
        $name = $city if !$name;

        $name = &select_city_name( $city, $name, 'en' );
        push( @data, [ $city, $name, $lang, $x, $y ] );

    }
    close $fh;

    return @data;
}

sub bbbike2kml_header {
    print <<EOF;
<?xml version="1.0" encoding="UTF-8"?>
<kml xmlns="http://earth.google.com/kml/2.2">
<Document>
  <name>BBBike @ World - a Cycle Route Planner</name>
  <description><![CDATA[List of all Cities supported by <a href="http://www.bbbike.org/">BBBike @ World</a>.]]></description>
  <Style id="style1">
    <IconStyle>
      <Icon>
        <href>http://maps.google.com/mapfiles/ms/micons/pink-pushpin.png</href>
      </Icon>
    </IconStyle>
  </Style>
EOF
}

sub bbbike2kml_footer {
    print <<EOF;
</Document>
</kml>
EOF
}

sub bbbike2kml_placemarks {
    my @data = @_;

    foreach my $c ( sort { $a->[0] cmp $b->[0] } @data ) {
        my ( $city, $name, $lang, $x, $y ) = @$c;

        my $bbbike_url = "http://www.bbbike.org/cgi/$city"
          . ( $lang eq '' ? '.cgi' : ".$lang.cgi" );

        print <<EOF;
  <Placemark>
    <name>$city</name>
    <description><![CDATA[Start <span style="color:rgb(0, 0, 238);text-decoration:underline"><a href="$bbbike_url">BBBike @ $name</a></span>]]></description>
    <styleUrl>#style1</styleUrl>
    <Point>
      <coordinates>$x,$y,0.000000</coordinates>
    </Point>
  </Placemark>
EOF
    }
}

sub bbbike2kml {
    my @data = @_;

    &bbbike2kml_header;
    &bbbike2kml_placemarks(@data);
    &bbbike2kml_footer;
}

my $db = $ARGV[0];
die &usage if !$db;

my @data = &extract_coords($db);
&bbbike2kml(@data);

