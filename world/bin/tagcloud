#!/usr/local/bin/perl
# Copyright (c) 2009-2010 Wolfram Schneider, http://bbbike.org
#
# tagcloud - create a tagcloud for bbbike @ world cities

use HTML::TagCloud 0.34;
use Getopt::Long;

use lib '.';
use lib './world/bin';
use BBBikeWorldDB;

use strict;
use warnings;

my $debug         = 1;      # 0: quiet, 1: normal, 2: verbose
my $selected_area = 'de';
my $level         = 12;
my $city_lang     = 'de';

sub usage () {
    <<EOF;
usage: $0 [--debug={0..2}] [--level=number ] [ --area=area ] < population.csv

--debug=0..2    debug option
--area=		de | eu | other
--lang=		de | en
--level=$level
EOF
}

# several tagclouds on one page
sub id2class {
    my $data = shift;
    $data =~ s,<div id="htmltagcloud">,<div class="htmltagcloud">,;
    return $data;
}

GetOptions(
    "debug=i" => \$debug,
    "area=s"  => \$selected_area,
    "level=i" => \$level,
    "lang=s"  => \$city_lang,
) or die usage;

my $cloud = HTML::TagCloud->new( levels => $level );

my $db = BBBikeWorldDB->new( 'database' => '/dev/stdin' );

foreach my $city ( $db->list_cities ) {
    my $c = $db->city->{$city};

    next if $c->{"area"} ne $selected_area;

    my $name =
      $db->select_city_name( $c->{"city"}, ( $c->{"name"} || $c->{"city"} ),
        $city_lang );
    my $link = "/$city/";

    $cloud->add( $name, $link, $c->{"population"} );
}

print &id2class( $cloud->html ), "\n";

