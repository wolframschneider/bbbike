#!/usr/local/bin/perl

use HTML::TagCloud 0.34;
use Getopt::Long;
use strict;
use warnings;

my $debug         = 1;      # 0: quiet, 1: normal, 2: verbose
my $selected_area = 'de';
my $level         = 12;
my $city_lang     = 'de';

sub usage () {
    <<EOF;
usage: $0 [--debug={0..2}] [--level=number ] [ --area=area ] < population.csv

--debug=0..2    debug option
--area=		de | eu | other
--lang=		de | en
--level=$level
EOF
}

# several tagclouds on one page
sub id2class {
    my $data = shift;
    $data =~ s,<div id="htmltagcloud">,<div class="htmltagcloud">,;
    return $data;
}

# select city name by language
sub select_city_name {
    my $name = shift or die "No city name given!\n";
    my $city_lang = shift || "de";

    my %hash;
    $hash{ALL} = $name;
    foreach my $n ( split /\s*,\s*/, $name ) {
        my ( $lang, $city_name ) = split( /!/, $n );
        if ($city_name) {
            $hash{$lang} = $city_name;
        }

        # no city language defined, use default
        else {
            $hash{ALL} = $lang;
        }
    }

    #use Data::Dumper; warn Dumper( \%hash );
    return exists( $hash{$city_lang} ) ? $hash{$city_lang} : $hash{ALL};
}

GetOptions(
    "debug=i" => \$debug,
    "area=s"  => \$selected_area,
    "level=i" => \$level,
    "lang=s"  => \$city_lang,
) or die usage;

my $cloud = HTML::TagCloud->new( levels => $level );

while (<>) {
    chomp;
    next if /^\s*#/;

    my ( $city, $name, $lang, $area, $coord, $population ) = split(/:/);
    next if $city eq 'dummy';

    $lang       = "de"  if !$lang;
    $area       = "de"  if !$area;
    $population = 1     if $population < 1;
    $name       = $city if !$name;

    next if $area ne $selected_area;
    $name = &select_city_name( $name, $city_lang );

    my $link = "cgi/$city" . ( $lang eq 'de' ? '' : ".$lang" ) . '.cgi';
    $cloud->add( $name, $link, $population );
}

print &id2class( $cloud->html ), "\n";

